#!/usr/bin/env zsh


# This file is part of mydot.

# mydot is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, version 3.

# mydot is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with mydot.  If not, see <https://www.gnu.org/licenses/>.

# Copyright (c) 2020-2021, Maciej BarÄ‡ <xgqt@riseup.net>
# Licensed under the GNU GPL v3 License

#          _
#  _______| |__  _ __ ___
# |_  / __| '_ \| '__/ __|
#  / /\__ \ | | | | | (__
# /___|___/_| |_|_|  \___|
# ~/.config/zsh/zshrc

# shellcheck disable=1090,2016,2034


# >>> Common settings

# Source necessary functions
profile="${HOME}/.profile"
[ -e "${profile}" ] && source "${profile}"

# Color
use_color=true

# Hooks
autoload -U add-zsh-hook


# >>> History

# Necessary to save history
HISTFILE="${ZCACHEDIR}/history"
HISTSIZE=50000
SAVEHIST="${HISTSIZE}"

# Ignore duplicates
setopt hist_ignore_all_dups

# Ignore space
setopt hist_ignore_space

# Save immediatelly, share history between terminals
setopt share_history


# >>> Completion

# Load compinit
# keep this first
autoload -U compinit
compinit -d "${ZCACHEDIR}/compdump"

# Advanced tab-completion
[ -n "${LS_COLORS}" ] && zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu select
zstyle ':completion:*:descriptions' format '%U%B%d%b%u'
zstyle ':completion:*:warnings' format '%BSorry, no matches for: %d%b'

# Correction
setopt correctall

# Extended globbing
setopt extendedglob

# Glob hidden files
setopt globdots

# Racket completion
source_file "/usr/share/racket/pkgs/shell-completion/racket-completion.zsh"


# >>> Key bindings

# Kill & Yank
# for this set Emacs key bindings
# keep this first
bindkey -e

# Ctrl-left and Ctrl-right
bindkey "^[[1;5D" backward-word
bindkey "^[[1;5C" forward-word

# Del, Home and End
bindkey "^[[3~" delete-char
bindkey "^[[F" end-of-line
bindkey "^[[H" beginning-of-line

# Remove C-d binding (list-choices/delete-char-or-list)
bindkey -r "^D"

# Reverse search
bindkey "^R" history-incremental-search-backward


# >>> Prompt theme

# VCS (git) support in prompt
setopt prompt_subst
autoload -Uz vcs_info
precmd_vcs_info() {
    vcs_info
}
precmd_functions+=( precmd_vcs_info )
zstyle ':vcs_info:git:*' formats "%b"

# Prompt init
autoload -U promptinit
promptinit

# Source appropriate theme
if [ -z "${DISPLAY}" ]
then
    # theme in tty
    source_file "${ZDOTDIR}/tty.zsh-theme"
else
    # theme in emulators
    source_file "${ZDOTDIR}/emu.zsh-theme"
fi


# >>> Plugins

# ZSH plugins directory
if [ -d "${ZDOTDIR}" ]
then
    ZPLUGDIR="${ZDOTDIR}/plugins"
    mkdir -p "${ZPLUGDIR}"
fi

# Autosuggestions
# https://github.com/zsh-users/zsh-autosuggestions
source_file "${ZPLUGDIR}/autosuggestions/zsh-autosuggestions.zsh"
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=6"

# Calculator
autoload -U zcalc
ZCALCPROMPT="(ZCALC)[%1v]> "
alias zc >/dev/null && unalias zc
alias zcalc >/dev/null && unalias zcalc
# WORKAROUND: History hack
#             set HOME and ZDOTDIR to ZCACHEDIR while invoking zcalc
# https://github.com/zsh-users/zsh/blob/a7d5d239e6ab729515083a88cfaf955e078c1685/Functions/Misc/zcalc#L128
rbind zcalc zcalc 'HOME=${ZCACHEDIR} ZDOTDIR=${ZCACHEDIR} zcalc'
zc() {
    ( [ -n "${1}" ] && zcalc -e "${@}" ) || zcalc
}
rbind zc zc 'noglob zc'

# K - Directory listings for zsh with git features
# https://github.com/supercrabtree/k
if ! command_exists k && ! alias k >/dev/null
then
    source_file "${ZPLUGDIR}/k/k.sh" && rbind k k 'k -Ah'
fi

# Syntax highlighting
# https://github.com/zsh-users/zsh-syntax-highlighting
source_file "${ZPLUGDIR}/syntax-highlighting/zsh-syntax-highlighting.zsh"

# Z - Quick directory navigation
source_file "${ZPLUGDIR}/z/zsh-z.plugin.zsh"
_Z_DATA="${ZCACHEDIR}/z"


# >>> Completions

if [ -d "${ZDOTDIR}/completions" ]
then
    for _completion in "${ZDOTDIR}/completions"/?*
    do
        source_file "${_completion}"
    done
    unset _completion
fi


# >>> ZSH tweaks

# After changing directory: execute 'ls'
chpwd() {
    ls
}

# Change the window title of X terminals
_term_title() {
    emulate -L zsh
    setopt prompt_subst

    local title_string="%n@%m: %~"

    case "${TERM}"
    in
        dumb )
            return
            ;;
        ansi | cygwin | putty* | rxvt* | xterm* )
            # window name
            print -Pn "\e]2;${title_string}\a"
            # tab name
            print -Pn "\e]1;${title_string}\a"
            ;;
        tmux* | screen* )
            # hardstatus
            print -Pn "\ek${title_string}\e\\"
            ;;
    esac
}
add-zsh-hook precmd _term_title

# Chmod ZSH cache
if [ -d "${ZCACHEDIR}" ]
then
    find "${ZCACHEDIR}" -type f -exec chmod 600 {} \; 2>/dev/null
fi

# Command Editor
autoload -U edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

# Directory name to change dir
setopt autocd

# Do not beep
unsetopt beep

# Settings for appropriate terminal
case "${TERM}"
in
    dumb )
        # For some SSH sessions (Emacs)
        unsetopt zle
        unsetopt prompt_cr
        unsetopt prompt_subst
        unfunction precmd
        unfunction preexec
        PS1='$ '
        ;;
esac
