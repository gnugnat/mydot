#!/usr/bin/env zsh


# This file is part of mydot.

# mydot is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, version 3.

# mydot is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with mydot.  If not, see <https://www.gnu.org/licenses/>.

# Copyright (c) 2020-2021, Maciej BarÄ‡ <xgqt@riseup.net>
# Licensed under the GNU GPL v3 License

# ~/.config/zsh/zshrc

# shellcheck disable=1090,2016,2034


# Common settings

# Source necessary functions
profile="${HOME}/.profile"
[ -e "${profile}" ] && source "${profile}"

# Color
use_color=true

# Hooks
autoload -U add-zsh-hook

# ZSH plugins directory
if [ -d "${ZDOTDIR}" ]
then
    ZPLUGDIR="${ZDOTDIR}/plugins"
    mkdir -p "${ZPLUGDIR}"
fi


# Completion

# General Completions
# https://github.com/zsh-users/zsh-completions
fpath+="${ZPLUGDIR}/completions/src"

# Gentoo Completions
# https://github.com/gentoo/gentoo-zsh-completions
fpath+="${ZPLUGDIR}/gentoo-completions/src"

# Load compinit
# keep this first
autoload -U compinit
compinit -d "${ZCACHEDIR}/compdump"

# Advanced tab-completion
[ -n "${LS_COLORS}" ] &&
    zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu select
zstyle ':completion:*:descriptions' format '%U%B%d%b%u'
zstyle ':completion:*:warnings' format '%BSorry, no matches for: %d%b'

# Correction
setopt correctall

# Extended globbing
setopt extendedglob

# Glob hidden files
setopt globdots


# History

# Necessary to save history
HISTFILE="${ZCACHEDIR}/history"
HISTSIZE=50000
SAVEHIST="${HISTSIZE}"

# Ignore duplicates
setopt hist_ignore_all_dups

# Ignore space
setopt hist_ignore_space

# Save immediately, share history between terminals
setopt share_history


# Key bindings

# Kill & Yank
# for this set Emacs key bindings
# keep this first
bindkey -e

# Ctrl-left and Ctrl-right
bindkey "^[[1;5D" backward-word
bindkey "^[[1;5C" forward-word

# Del, Home and End
bindkey "^[[3~" delete-char
bindkey "^[[F" end-of-line
bindkey "^[[H" beginning-of-line

# Remove C-d binding (list-choices/delete-char-or-list)
bindkey -r "^D"

# Reverse search
bindkey "^R" history-incremental-search-backward


# Prompt theme

# VCS (git) support in prompt
setopt prompt_subst
autoload -Uz vcs_info
precmd_vcs_info() {
    vcs_info
}
precmd_functions+=( precmd_vcs_info )
zstyle ':vcs_info:git:*' formats "%b"

# Prompt init
autoload -U promptinit
promptinit

# Source appropriate theme
if [ -z "${DISPLAY}" ]
then
    # theme in tty
    source_file "${ZDOTDIR}/tty.zsh-theme"
else
    # theme in emulators
    source_file "${ZDOTDIR}/emu.zsh-theme"
fi


# Plugins

# Autosuggestions
# https://github.com/zsh-users/zsh-autosuggestions
source_file "${ZPLUGDIR}/autosuggestions/zsh-autosuggestions.zsh"
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=6"

# Calculator
source_file "${ZPLUGDIR}/calc/calc.zsh"

# K - Directory listings for zsh with git features
# https://github.com/supercrabtree/k
if ! command_exists k && ! alias k >/dev/null
then
    source_file "${ZPLUGDIR}/k/k.sh" && rbind k k 'k -Ah'
fi

# Syntax highlighting
# https://github.com/zsh-users/zsh-syntax-highlighting
source_file "${ZPLUGDIR}/syntax-highlighting/zsh-syntax-highlighting.zsh"

# Z - Quick directory navigation
# https://github.com/agkozak/zsh-z
source_file "${ZPLUGDIR}/z/zsh-z.plugin.zsh"
_Z_DATA="${ZCACHEDIR}/z"


# Miscellaneous tweaks

# After changing directory: execute 'ls'
chpwd() {
    ls
}

# Change the window title of X terminals
_term_title() {
    emulate -L zsh
    setopt prompt_subst

    local title_string="%n@%m: %~"

    case "${TERM}"
    in
        dumb )
            return
            ;;
        ansi | cygwin | putty* | rxvt* | xterm* )
            # window name
            print -Pn "\e]2;${title_string}\a"
            # tab name
            print -Pn "\e]1;${title_string}\a"
            ;;
        tmux* | screen* )
            # hardstatus
            print -Pn "\ek${title_string}\e\\"
            ;;
    esac
}
add-zsh-hook precmd _term_title

# Chmod ZSH cache
if [ -d "${ZCACHEDIR}" ]
then
    find "${ZCACHEDIR}" -type f -exec chmod 600 {} \; 2>/dev/null
fi

# Command Editor
autoload -U edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

# Directory name to change dir
setopt autocd

# Do not beep
unsetopt beep

# Settings for appropriate terminal
case "${TERM}"
in
    "" | dumb )
        # For some SSH sessions (Emacs)
        unsetopt zle
        unsetopt prompt_cr
        unsetopt prompt_subst
        PS1='(SH)> '
        ;;
esac


# Site

# Source additional files

_source_zsh_site() {
    local _s

    for _s in "${ZDOTDIR}/site"/?*
    do
        source_file "${_s}"
    done
}

[ -d "${ZDOTDIR}/site" ] && _source_zsh_site
