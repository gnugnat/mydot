#!/usr/bin/env zsh


# This file is part of mydot.

# mydot is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# mydot is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with mydot.  If not, see <https://www.gnu.org/licenses/>.

# Copyright (c) 2020, XGQT
# Licensed under the GNU GPL v3 License

#          _
#  _______| |__  _ __ ___
# |_  / __| '_ \| '__/ __|
#  / /\__ \ | | | | | (__
# /___|___/_| |_|_|  \___|
# ~/.config/zsh/zshrc


# >>> Common settings

profile="${HOME}/.profile"
[ -e "${profile}" ] && source "${profile}"


# >>> History

# Necessary to save history
HISTFILE="${ZCACHEDIR}/history"
HISTSIZE=50000
SAVEHIST="${HISTSIZE}"

# Ignore duplicates
setopt hist_ignore_all_dups

# Ignore space
setopt hist_ignore_space

# Save immediatelly, share history between terminals
setopt share_history


# >>> Completion

# Correction
setopt correctall

# Extendedglob
setopt extendedglob

# Glob hidden files
setopt globdots

# Load compinit
autoload -U compinit
compinit -d "${ZCACHEDIR}/compdump"

# Advanced tab-completion
zstyle ':completion:*' menu select
zstyle ':completion:*:descriptions' format '%U%B%d%b%u'
zstyle ':completion:*:warnings' format '%BSorry, no matches for: %d%b'
zstyle -e ':completion:*:default' list-colors 'reply=("${PREFIX:+=(#bi)($PREFIX:t)(?)*==34=34}:${(s.:.)LS_COLORS}")'

# Racket completion
source_file "/usr/share/racket/pkgs/shell-completion/racket-completion.zsh"


# >>> Key bindings

# Kill & Yank
# for this set Emacs key bindings
bindkey -e

# Ctrl-left and Ctrl-right
bindkey "^[[1;5D" backward-word
bindkey "^[[1;5C" forward-word

# Del, Home and End
bindkey "^[[3~" delete-char
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line

# Reverse search
bindkey "^R" history-incremental-search-backward

# Remove C-d binding (list-choices/delete-char-or-list)
bindkey -r "^D"


# >>> Prompt theme

# Prompt init
autoload -U promptinit
promptinit

# Source appropriate theme
if [ -z "${DISPLAY}" ]
then
    # theme in tty
    source_file "${ZDOTDIR}/tty.zsh-theme"
else
    # theme in emulators
    source_file "${ZDOTDIR}/emu.zsh-theme"
fi


# >>> Plugins

# Syntax coloring
source_file "${ZDOTDIR}/plugins/syntax-highlighting/zsh-syntax-highlighting.zsh"

# Autosuggestions
source_file "${ZDOTDIR}/plugins/autosuggestions/zsh-autosuggestions.zsh"

# Autosuggestions highlight style
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=6"

# Completion for kitty
if command_exists kitty
then
    kitty + complete setup zsh | source /dev/stdin
fi


# >>> ZSH tweaks

# Prompt Substring
# used by git_check
setopt PROMPT_SUBST

# Color
use_color=true

# Directory name to change dir
setopt autocd

# Do not beep
unsetopt beep

# Command Editor
autoload -U edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

case ${TERM} in
    xterm* )
        # Change the window title of X terminals
        precmd() {
            print -Pn "\e]0;${USER}@${HOST}:${PWD/#$HOME/\~}\a"
        }
        ;;
    dumb )
        # For some SSH sessions (Emacs)
        unsetopt zle
        unsetopt prompt_cr
        unsetopt prompt_subst
        unfunction precmd
        unfunction preexec
        PS1='$ '
        ;;
esac

# ls after changing directory
chpwd() ls
